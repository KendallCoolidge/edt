/*******************************************************************************
 * Copyright Â© 2012, 2013 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * IBM Corporation - initial API and implementation
 *
 *******************************************************************************/
package org.eclipse.edt.eunit.runtime;

import eglx.xml.XmlLib;

// basic library
library TestExecutionLib
	
	private resultRootSum ResultSummaryRoot;
		
	const STEXTS String[] = ["Passed.", "Failed.", "Error.", "Not run.", "Bad."];
			
	/**
	* driver program or handler can call this to start the test
	*/
	function testMain(exeLibTestMtd executeLibTestMethod in, td TestDescription in, bwriteStdOut boolean in)
		//clear the status and log record to hold result in LogResult Library
		LogResult.clearResults();	
		
		//run the test case		
		ms MultiStatus = exeLibTestMtd(td.name);
		
		writeResults(td, ms, bwriteStdOut);		
	end
	
	function writeResults(td TestDescription in, ms MultiStatus in, bwriteStdOut boolean in)
		//convert multiStatus to status		
		s Status = calculateStatus(ms);				
		
		stdOutLog Log = LogResult.getLog();
				
		if(bwriteStdOut)
			delta int = 1;
			keyCode int = s.code + delta;
			description string = STEXTS[keyCode];
			SysLib.writeStdOut("Status code: " + s.code + " - " + description);
			SysLib.writeStdOut("Status reason: " + s.reason);
			SysLib.writeStdOut("");
			xml2 String = XMLLib.convertToXML(stdOutLog, true);
			SysLib.writeStdOut("Out record: \r\n" + xml2);
		end
		
		//need working directory or testRoot dir, need full package name
		//to construct the location and name of the result file
		path String = "This is the path where the output file should be";
		WriteResultLib.writeResult(s, ms, stdOutLog, td, this.resultRootSum); 			
	end	
	
	function writeResultSummary(expectedTestCnt int in, startTS String in)
		endRunTS timestamp("yyyyMMddHHmmssffffff");
		this.resultRootSum.expCnt = expectedTestCnt;
		this.resultRootSum.startTS = startTS;
		this.resultRootSum.endRunTS = endRunTS;
		WriteResultLib.persistResultRootSummary(this.resultRootSum);
	end	
	
	/**
	* helper function to run test that has mulitple test case methods
	*/
	function runMultiTest(testMethods String[] in, runTestMtd runTestMethod[] in, testName String in) returns (MultiStatus)
		ms MultiStatus;		
		testId String;

		testName1 String =  testName + "::";				
		testVariationCnt int = testMethods.getSize();
		ms.expectedCnt = testVariationCnt;
				
		for (i int from 1 to testVariationCnt by 1)
			testId = testName1 + testMethods[i];
			try
				sysLib.writeStdOut("Running test: " + testId);
				runTestMtd[i]();
			onException (assertion AssertionFailedException)		
				//prepend the assertion failure error message to Status
				sysLib.writeStdOut("AssertionFail - " + assertion.message);
			onException (testException AnyException)
				//caught the exception from the test 
				expMsg String = "uncaught exception for: " + testId; 
				expMsg += ConstantsLib.NEWLINE + "    => " + testException.messageID + ": " + testException.message;				
				//StatusLib.error(expMsg, s);
				LogResult.error(expMsg);
			end
			ms.addStatus(testId);
		end
						
		return (ms);
								
	end	
		
	/**
	* get the status from the MultiStatus
	*/
	private function calculateStatus(ms MultiStatus in) returns (Status)		
		retStat Status;
		summary String;
		if(ms.testCnt == 0)
			summary = "No tests cases found " + ConstantsLib.NEWLINE;
		else
			summary = "test case count: " + ms.testCnt;
			if(ms.passedCnt > 0)
				if(ms.passedCnt == ms.testCnt)
					summary += " - all passed.";
				else
					summary += ConstantsLib.NEWLINE + "passed: " + ms.passedCnt;
				end
			end
			
			if(ms.failedCnt > 0 )
				if(ms.failedCnt == ms.testCnt)
					summary += " - all failed.";
				else
					summary += ConstantsLib.NEWLINE + "failed: " + ms.failedCnt;
				end
			end
			
			if(ms.errCnt > 0)
				if(ms.errCnt == ms.testCnt)
					summary += " - all have error.";
				else
					summary += ConstantsLib.NEWLINE + "error: " + ms.errCnt;
				end
			end
			
			if(ms.notRunCnt > 0)
				if(ms.notRunCnt == ms.testCnt)
					summary += " - all tests are skipped.";
				else
					summary += ConstantsLib.NEWLINE + "skipped: " + ms.notRunCnt;
				end
			end
			
			if(ms.badCnt > 0 )
				if(ms.badCnt == ms.testCnt)
					summary += " - all have bad status.";
				else
					summary += ConstantsLib.NEWLINE + "bad status: " + ms.badCnt;
				end
			end
		end
		
		
		if(ms.badCnt > 0)
			summary += ConstantsLib.NEWLINE + "first bad test case result found: " + ms.firstFailedTestName;
			setStatusRecordHelper(retStat, ConstantsLib.SERROR, summary);
			return (retStat);
		end
					
		if(ms.errCnt > 0 )
			summary += ConstantsLib.NEWLINE + "first test case with error: " + ms.firstFailedTestName;
			setStatusRecordHelper(retStat, ConstantsLib.SERROR, summary);	
			return (retStat);
		end	
		
		if(ms.failedCnt > 0)
			summary += ConstantsLib.NEWLINE + "first test case failure: " + ms.firstFailedTestName;
			setStatusRecordHelper(retStat, ConstantsLib.SFAILED, summary);	
			return (retStat);		
		end
		
		if(ms.notRunCnt > 0)
			summary += ConstantsLib.NEWLINE + "first test case skipped: " + ms.firstFailedTestName;
			setStatusRecordHelper(retStat, ConstantsLib.SNOT_RUN, summary);	
			return (retStat);				
		else
			setStatusRecordHelper(retStat, ConstantsLib.SPASSED, summary);	
			return (retStat);			
		end
	end
	
	private function setStatusRecordHelper(s Status out, c int in, r String in)
		s.code = c;
		s.reason = r;
	end
			
end
