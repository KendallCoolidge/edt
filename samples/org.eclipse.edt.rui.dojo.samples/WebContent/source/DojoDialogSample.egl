/*******************************************************************************
 * Copyright Â© 2009, 2013 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * IBM Corporation - initial API and implementation
 *
 *******************************************************************************/
package dojo.samples.perwidget;

import org.eclipse.edt.rui.widgets.Box;
import org.eclipse.edt.rui.widgets.Div;
import org.eclipse.edt.rui.widgets.FloatRight;
import org.eclipse.edt.rui.widgets.HTML;
import dojo.widgets.DojoButton;
import dojo.widgets.DojoDialog;
import eglx.ui.rui.Event;
import eglx.ui.rui.RUIHandler;

handler DojoDialogSample type RUIHandler{initialUI =[ui]}
            
    dialog DojoDialog {
    	title = "Test Dialog Box",
    	padding = 20, 
    	backgroundColor = "lightblue",
    	draggable = true, 
    	children = [ content ]
    };
    content Box { columns=1 };
	
    info Div { 
    	padding = 10, width=260, backgroundColor = "lightyellow", 
    	children = [
	    	new HTML { text = "<p>The dialog content goes here. Notice this dialog has its padding set to 20 pixels (the light blue bar around the yellow box).</p>" +
	    		"<p>You can drag this dialog and close it with the cancel button shown in the title bar above, or with the 'Yes' and 'No' buttons added below by this example...</p>" +
	    		"<p>The width for this dialog is set to 300px, but height is unspecified. Therefore, the dialog will grow to adjust its children."
	    	},
    		new DojoButton { text = "Reduce dialog height", onClick ::= toggle, marginTop = 11 }
    	]
    };
    
    buttonBar FloatRight { marginTop=9, children = [
		new Box { children = [
		    new DojoButton {
		    	text = "Yes", onClick ::= hideDialog
		    },
		    new DojoButton {
		    	text = "No", marginRight=0, onClick ::= hideDialog
		    }
		]}
	]};

    ui Div { 
    	padding = 30, children = [
			new HTML { 
				text = "Dojo Dialog Sample", paddingBottom = 20, fontSize = "20px" 
			},
			new HTML { 
				text = "Click the button to show the dialog.", paddingBottom = 15 
			},
    		new DojoButton { 
    			text = "Show Dialog", onClick ::= showDialog
    		}
    	]
    };

    function showDialog(e Event in)
        content.children = [
		    info,
			buttonBar
		];
		dialog.showDialog();
    end

    function hideDialog(e Event in)
        dialog.hideDialog();
    end

    function toggle(e Event in)
    	content.children = [
        	new HTML { 
	    		padding = 20, marginRight=2, backgroundColor = "lightYellow", text = "This dialog is small."
	    	}, 
	    	buttonBar
        ];
    end

end
