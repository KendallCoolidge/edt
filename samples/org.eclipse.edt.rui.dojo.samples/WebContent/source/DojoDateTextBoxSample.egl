/*******************************************************************************
 * Copyright Â© 2009, 2012 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * IBM Corporation - initial API and implementation
 *
 *******************************************************************************/
package dojo.samples.perwidget;

import org.eclipse.edt.rui.widgets.Box;
import org.eclipse.edt.rui.widgets.HTML;
import dojo.widgets.DojoComboBox;
import dojo.widgets.DojoDateTextBox;
import eglx.lang.DateTimeLib;
import eglx.ui.rui.Event;
import eglx.ui.rui.RUIHandler;

Handler DojoDateTextBoxSample type RUIHandler { InitialUI = [ ui ] }
	
	dateBox DojoDateTextBox { 
		value = new Date{}, 
		errorMessage = "Invalid Date.", 
		formatLength = dojo.widgets.DojoLib.DATEBOX_FORMAT_SHORT,
		onChange ::= showDate,
		width = "200px"
	};
					 	
	dateDetails HTML {
		marginTop = 20 
	};
	
	patternChooser DojoComboBox {
		marginLeft = 30,
		values = [
			dojo.widgets.DojoLib.DATEBOX_FORMAT_SHORT,
			dojo.widgets.DojoLib.DATEBOX_FORMAT_MEDIUM,
			dojo.widgets.DojoLib.DATEBOX_FORMAT_LONG,
			dojo.widgets.DojoLib.DATEBOX_FORMAT_FULL
		],
		onChange ::= switchPattern
	};
	
	ui Box{ padding=20, columns=1, children = [ 
		new HTML { 
			text = "Dojo Date Text Box Sample", fontSize = "20px" 
		},
		new HTML { 
			text="<p>A Dojo date text box lets the user choose a date. <p>" +
				"The error message can be customized. <br>" +
				"The format also can customized using formatLength options of 'short', 'medium', 'long' or 'full'.<br>" 
		},
		new Box { paddingTop=10, children= [
			new HTML {
				text="Enter a date: ", marginRight=5
			}, 
			dateBox,
			patternChooser
		]},
		dateDetails 
	]};
	
	function switchPattern(e Event in)
		currentPattern String = dateBox.formatLength;
		try
			dateBox.formatLength = patternChooser.value;
			dateBox.value = dateBox.value;
		onException (exc AnyException)
			patternChooser.value = currentPattern;
			dateBox.formatLength = currentPattern;
		end
	end
	
	function showDate(e Event in)
		dateDetails.text = "You chose the following date: " + dateBox.value;
	end

end
