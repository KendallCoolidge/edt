/*******************************************************************************
 * Copyright ï¿½ 2011 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * IBM Corporation - initial API and implementation
 *
 *******************************************************************************/
package dojo.widgets;

import eglx.ui.rui.Event;
import eglx.ui.rui.RuiLib;
import eglx.ui.rui.Widget;

library DojoDialogLib {}

	const ID_OK String = "OK";
	const ID_CANCEL String = "CANCEL";
	const ID_YES String = "YES"; 
	const ID_NO String = "NO";
	const ID_RETRY String = "RETRY";  

	function showAlert(message String in) returns (DojoDialog)
		buttons DojoButton[] = [ createButton("OK", ID_OK, null) ];
		content Widget = createContents( message, null );
		
		return(createDialog(null, message, content, buttons));
	end	

	function showMessage(title String? in, message String in, callback dialogButtonClicked?) returns (DojoDialog)
		buttons DojoButton[] = [ createButton("OK", ID_OK, callback) ];
		content Widget = createContents( message, null );
		
		return(createDialog(title, message, content, buttons));
	end	
	
	function showQuestion(title String? in, message String in, callback dialogButtonClicked?) returns (DojoDialog)
		buttons DojoButton[] = [ createButton("Yes", ID_YES, callback), createButton("No", ID_NO, callback) ];
		content Widget = createContents( message, "images/question_32.png" );
		
		return(createDialog(title, message, content, buttons));
	end	
		
	function showWarning(title String? in, message String in, callback dialogButtonClicked?) returns (DojoDialog)
		buttons DojoButton[] = [ createButton("OK", ID_OK, callback) ];
		content Widget = createContents( message, "images/warning_32.png" );
		
		return(createDialog(title, message, content, buttons));
	end

	function showError(title String? in, message String in, callback dialogButtonClicked?) returns (DojoDialog)
		buttons DojoButton[] = [ createButton("OK", ID_OK, callback) ];
		content Widget = createContents( message, "images/error_32.png" );
		
		return(createDialog(title, message, content, buttons));	
	end
	
	function showProgress(title String? in, message String in, allowCancel boolean in, callback dialogButtonClicked?) returns (DojoDialog)
		buttons DojoButton[];
		if (allowCancel)
			buttons = [ createButton("Cancel", ID_CANCEL, callback) ];
		end
		content Widget = createContents( message, "images/progress_32.gif" );
		
		return(createDialog(title, message, content, buttons));			
	end
	
	function showRetryCancel(title String? in, message String in, callback dialogButtonClicked?) returns (DojoDialog)
		buttons DojoButton[] = [ createButton("Retry", ID_RETRY, callback), createButton("Cancel", ID_CANCEL, callback) ];
		content Widget = createContents( message, "images/error_32.png" );
		
		return(createDialog(title, message, content, buttons));	
	end
	
	function promptForValue(title String? in, message String in, callback promptSupplied) returns (DojoDialog)
		promptField DojoTextField { width = "300px", style = "max-width: 500px"  };
		
		okButton DojoButton =  createButtonForPrompt("OK", ID_OK, promptField);
		cancelButton DojoButton =  createButtonForPrompt("Cancel", ID_CANCEL, promptField);		
		buttons DojoButton[] = [ okButton, cancelButton  ];
		
		fDict Dictionary; 
		fDict.okButton = okButton;
		fDict.cancelButton = cancelButton;
		fDict.callback = callback;
		promptField.setUserData(fDict);
		
		row1 Widget { tagName = "tr", children = [new Widget { tagName = "td", innerHTML = message, style = "padding: 4px; vertical-align: middle; max-width: 500px" }] };
		row2 Widget { tagName = "tr", children = [new Widget { tagName = "td", children = [ promptField] } ] };
		
		body Widget { tagName = "tbody", children = [ row1, row2 ] };
		table Widget { tagName = "table", children = [ body ], style = "max-width: 500px", onKeyDown ::= tableKeyDown };
		table.setAttribute("border", "0");
		table.setUserData(fDict);
		
		if (title == null)
			title = RuiLib.getTitle();
		end
						
		dialog DojoDialog { title = title, children = [ table ], buttons = buttons };
		dialog.showDialog();	
		
		return (dialog);
	end		
	
	function tableKeyDown(e Event in) 
		fDict Dictionary = e.widget.getUserData();
		if (e.ch == 13 && fDict.okButton != null)
			button DojoButton = fDict.okButton;
			e.widget = button;
			promptButtonClicked(e);
		end
		if (e.ch == 27 && fDict.cancelButton != null)
			button DojoButton = fDict.cancelButton;
			e.widget = button;
			promptButtonClicked(e);
		end
	end	
	
	private function createDialog(title string? in,message String in, content widget in, buttons DojoButton[] in) returns(DojoDialog)
		if (title == null)
			title = RuiLib.getTitle();
		end
		
		dialog DojoDialog { title = title, children = [ content ], buttons = buttons};
		
		//To avoid overlap the 'x' of dialog
		dialog.style = "min-width:" + title.length() + "em";
		dialog.showDialog();	
		
		return(dialog);
	end
		
	function createButton(text String in, id String in, callback dialogButtonClicked?) returns (DojoButton)
		button DojoButton {value = id, text = text, onClick ::= buttonClicked };
		
		bDict Dictionary; 
		bDict.functionId = id;
		bDict.callback = callback;
		button.setUserData(bDict);
		
		return (button);
	end	
	
	function createButtonForPrompt(text String in, id String in, promptField DojoTextField) returns (DojoButton)
		button DojoButton {value = id, text = text, onClick ::= promptButtonClicked};
		
		buttonDict Dictionary;
		buttonDict.functionId = id;
		buttonDict.promptField = promptField;
		button.setUserData(buttonDict);
		
		return (button);
	end

	function createContents(message String in, imagePath String? in) returns (Widget)
		cells Widget { tagName = "tr", children = [] };
		body Widget { tagName = "tbody", children = [ cells ] };
		table Widget { tagName = "table", cssClass = "EGLDojoDialogLibTable", children = [ body ], style = "max-width: 500px" };
		table.setAttribute("border", "0");
						
		if (imagePath != null) 
			image Widget { tagName = "img" };
			image.setAttribute("src", imagePath);
			cells.appendChild(new Widget { tagName = "td", width = 32, children = [ image ], style = "padding: 4px; vertical-align: middle" });
		end		
		cells.appendChild(new Widget { tagName = "td", innerHTML = message, style = "padding: 4px; vertical-align: middle; max-width: 500px" } );

		return (table);
	end
	
	private function buttonClicked(e Event in)
		button DojoButton = (e.widget as DojoButton);	
		
		dialog DojoDialog = findParentDialog(e.widget);
		
		buttonDict Dictionary = button.getUserData();
		
		hide boolean = true;
		if(buttonDict.callback != null)
			callback dialogButtonClicked = buttonDict.callback;
			functionId String= buttonDict.functionId;
			hide = callback(functionId, e);
		end					
		
		if (hide)
			dialog.hideDialog();
		end				
	end
	
	private function promptButtonClicked(e Event in) 
		button DojoButton = (e.widget as DojoButton);		
		
		buttonDict Dictionary = button.getUserData();
		promptField DojoTextField = buttonDict.promptField;
		
		dialog DojoDialog = findParentDialog(e.widget);
		
		hide boolean = true;
		
		if(promptField.getUserData() != null)
			fDict Dictionary = promptField.getUserData();
			if (fDict.callback != null)			
				callback promptSupplied = fDict.callback;
				if (buttonDict.functionId == ID_CANCEL)
					hide = callback(null, e);
				else
					hide = callback(promptField.text, e);			
				end
			end	
		end
		
		if (hide)
			dialog.hideDialog();
		end						
	end
	
	function findParentDialog(widget Widget in) returns (DojoDialog)
		parent Widget = widget.parent;
		while (parent != null && !(parent isa DojoDialog))
			parent = parent.parent;			
		end
		return (parent);
	end
	
end

delegate dialogButtonClicked(buttonId String in, e Event in) returns (boolean)
	
end

delegate promptSupplied(value String? in, e Event in) returns (boolean)

end
