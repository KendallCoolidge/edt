/*******************************************************************************
 * Copyright Â© 2011 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * IBM Corporation - initial API and implementation
 *
 *******************************************************************************/
package org.eclipse.edt.rui.mvc;



library InternalTimestampFormatter
	use InternalMessageLib;
	util InternalFormattingUtil{};
	
	function format(input string in, format string in, typeChkMsgKey string in) returns(string)
		if (input == "")
			return("");
		end
		
		try
			if (format != "")
				// A timestamp format has been specified, convert the string to a timestamp so that it can be formatted
				timestampInput timestamp = input;
				return(stringLib.format(timestampInput, format));	
			else
				// If there is no timestamp format, let the default string conversion happen
				return(input);
			end
		onException(exception AnyException)
			errorFormat String = format;
			if (errorFormat == "")
				//TODO: do not hard code this format - access it from ETimestamp
				errorFormat = "yyyy-MM-dd HH:mm:ss";
			end
			throw new AnyException {message = getRuntimeMessage(typeChkMsgKey, "CRRUI2717E", [errorFormat]), messageId = "CRRUI2717E"};
		end
	end
	
	function unformat(input string in, format string in, typeChkMsgKey string in) returns(string)
		if (input == "")
			return("");
		end
		
		if(format != "")
			// A timestamp format has been specified, attempt to convert the string to a timestamp 
			result timestamp? = util.asTimestamp(input, format, true);
			
			if(result == null)
				// The conversion failed
				errorFormat string = format;
				throw new AnyException {message = getRuntimeMessage(typeChkMsgKey, "CRRUI2717E", [errorFormat]), messageId = "CRRUI2717E"};
			else
				return(result);
			end
		else
			try
				// If there is no timestamp format, let the default string conversion happen
				result timestamp = input;
				return (result);
			onException (exception AnyException)
				// TODO: do not hard code this format - access it from ETimestamp
				errorFormat string = "yyyy-MM-dd HH:mm:ssssss";
				throw new AnyException {message = getRuntimeMessage(typeChkMsgKey, "CRRUI2717E", [errorFormat]), messageId = "CRRUI2717E"};
			end	
		end
	end
end
