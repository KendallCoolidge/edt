/*******************************************************************************
 * Copyright Â© 2012 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * IBM Corporation - initial API and implementation
 *
 *******************************************************************************/
package org.eclipse.edt.rui.mvc;

library InternalTimeFormatter
    use InternalMessageLib;

    function format(input string in, format string in, typeChkMsgKey string in) returns(string)
        if(input == "")
            return("");
        end
        
        // Input must be in the format defined by ETime.asTime()
        try
            if(format != "")
            	// A time format has been specified, convert the string to a time so that it can be formatted
                timeInput time = input;
                return(stringLib.format(timeInput,format));
            else
            	// If there is no time format, let the default string conversion happen
                return(input);
            end
        onException(exception AnyException)
            errorFormat string = format;
            if(errorFormat == "")
            	//TODO: do not hard code this format - access it from ETime
                errorFormat = "HH:mm:ss";
            end
            throw new AnyException{message = getRuntimeMessage(typeChkMsgKey, "CRRUI2717E", [errorFormat]), messageId = "CRRUI2717E"};
        end
    end

    function unformat(input string in, format string in, typeChkMsgKey string in) returns(string)
        if(input == "")
            return("");
        end

        if(format != "")
        	util InternalFormattingUtil;
        	
        	// A time format has been specified, attempt to convert the string to a time 
			result time? = util.asTime(input, format, true);
        	
            if(result == null)
				// The conversion failed
				errorFormat string = format;
				throw new AnyException {message = getRuntimeMessage(typeChkMsgKey, "CRRUI2708E", [errorFormat]), messageId = "CRRUI2708E"};
			else
				return(result);
			end
        else
            try
            	// If there is no time format, let the default string conversion happen
                result time = input;
                return(result);
            onException(exception AnyException)
                // TODO: do not hard code this format - access it from ETime
                errorFormat string = "HH:mm:ss";
                throw new AnyException{message = getRuntimeMessage(typeChkMsgKey, "CRRUI2717E", [errorFormat]), messageId = "CRRUI2717E"};
            end	
        end
    end
end
