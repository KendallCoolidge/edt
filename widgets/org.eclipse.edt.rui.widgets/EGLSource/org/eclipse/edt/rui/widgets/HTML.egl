/*******************************************************************************
 * Copyright Â© 2011 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * IBM Corporation - initial API and implementation
 *
 *******************************************************************************/
package org.eclipse.edt.rui.widgets;

import eglx.http.HttpRest;
import eglx.ui.rui.Document;
import eglx.ui.rui.RUIWidget;
import eglx.ui.rui.VEProperty;
import eglx.ui.rui.VEWidget;



handler HTML type RUIWidget{ tagName = "div", onConstructionFunction = start,
		cssFile = "css/org.eclipse.edt.rui.css", 
		@VEWidget{
				category = "Display and Input",
				template = "${typeName}{ text=\"HTML\" }",
				displayName = "HTML",
				smallIcon = "icons/ctool16/html.gif",
				largeIcon = "icons/ctool24/html.gif"
			}   }

	text String { @EGLProperty { getMethod=getText, setMethod=setText }, @VEProperty{} };
	src String { @EGLProperty { getMethod=getSrc, setMethod=setSrc }, @VEProperty{} };
	
	private contextURL String;
	private widgetOrientationThis String = "";
	
	function start()
		location String = document.location;
		index0 int = location.indexOf("//") ;
		index0 = location.indexOf("/", index0 + 2);
		index1 int = location.indexOf("/", index0 + 1);
		if (index1 != 0)
			index0 = index1;
		end
	    contextURL = location[1:index0];
	end
	
	function setText(text string in)
		this.text = text;
		innerHTML = text;
	end
	
	function getText() returns (string)
		return (this.text);
	end

	function setSrc(src string in)
		if (src.length() < 4 || src[1:4].toLowerCase() != "http")
           call RestLib.invokeGet(contextURL + src) using new HttpRest returning to setText;
        else
        	call RestLib.invokeGet(src) using new HttpRest returning to setText;
		end
		this.src = src;
	end
	
	function getSrc() returns (string)
		return (src);
	end

	function getWidgetOrientation() returns (String) 
		return (this.widgetOrientationThis);		
	end
	
	function setWidgetOrientation(widgetOrientation String in) 
		this.widgetOrientationThis = widgetOrientation;	
		if(widgetOrientation == "RTL")
			this.style = "text-align: right;";
			if(this.getTextLayout() != "Visual")
				this.setAttribute("dir", "ltr");
			end				
		else
			this.style = "text-align: left;";		
		end
	end
end
