/*******************************************************************************
 * Copyright Â© 2011, 2013 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * IBM Corporation - initial API and implementation
 *
 *******************************************************************************/
package org.eclipse.edt.rui.widgets;

library DataGridFormatters

    function dollarSigns(cssClass string inout, value string inout, rowData any in)
        value = "$" + value;
    end

    function coloredNumbers(cssClass string inout, value string inout, rowData any in)
        if(value < 0)
            cssClass += " EglRuiDataGridCellNegative";
        else
            if(value == 0)
                cssClass += " EglRuiDataGridCellNull";
            else
                cssClass += " EglRuiDataGridCellPositive";
            end
        end
    end

    function addCommas(cssClass string inout, value string inout, rowData any in)
    	if (value == "")
			return;
		end
		len int = value.length();
		for (i int from 1 to len)
			s string = value[i:i];    			
    		if ( (s < "0" || s > "9") && s != "." && ( i != 1 || s != "-" ) )
    			return;
    		end
    	end
		preDecimal string;
		postDecimal string;
		decimalIndex int = value.indexOf(".");
		startIndex int = 1;
		if( value[1:1] == "-" )
			startIndex = 2;
		end
		if (decimalIndex > 1)
			preDecimal = value[1:decimalIndex - 1];
			if (value.length() > decimalIndex)
				postDecimal = value[decimalIndex + 1:value.length()];
			end 
		else
			if (decimalIndex == 1)
				if (value.length() > 1)
					postDecimal = value[2:value.length()];
				end 
			else
				preDecimal = value;
			end
		end

		separatorChar string = ",";
		for (i int from preDecimal.length() - 3 to startIndex decrement by 3)
			preDecimal = preDecimal[1:i] + separatorChar + preDecimal[i+1:preDecimal.length()];
		end
		value = preDecimal + "." + postDecimal;
    end

    function percentage(cssClass string inout, value string inout, rowData any in)
        value += "%";
    end

    function fontWeightItalic(cssClass string inout, value string inout, rowData any in)
        value = "<i>" + value + "</i>";
    end

    function fontWeightBold(cssClass string inout, value string inout, rowData any in)
        value = "<b>" + value + "</b>";
    end

    function fontWeightUnderlined(cssClass string inout, value string inout, rowData any in)
        value = "<u>" + value + "</u>";
    end

    function fontWeightBoldItalic(cssClass string inout, value string inout, rowData any in)
        value = "<i><b>" + value + "</b></i>";
    end

    function makeLink(cssClass string inout, value string inout, rowData any in)
        cssClass += " EglRuiDataGridCellLink";
        value = "<span onClick='" +
                "egl.org.eclipse.edt.rui.infobus.InfoBus.$inst.publish(\"grid.link\",egl.createWidget(this.parentNode.parentNode))'>" +
                value + "</span>";
    end

end
	
