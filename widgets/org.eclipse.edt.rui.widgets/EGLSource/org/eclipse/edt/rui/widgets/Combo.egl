/*******************************************************************************
 * Copyright Â© 2011 IBM Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * IBM Corporation - initial API and implementation
 *
 *******************************************************************************/
package org.eclipse.edt.rui.widgets;

import org.eclipse.edt.rui.history.HistoryHelper;

handler Combo type RUIWidget{tagName = "select", cssFile = "css/org.eclipse.edt.rui.css",
	@VEWidget{
		category = "Display and Input",
		template = "${typeName}{ values = [\"Combo\"] }",
		displayName = "Combo",
		smallIcon = "icons/ctool16/combo.gif",
		largeIcon = "icons/ctool24/combo.gif"
	},	
	@MVCView{
		retrieveViewHelper = getValueAsText,
		publishHelper = setValueAsText
	},
	@VEDataTemplate{
		dataTemplates = [
			@DataTemplate{
				name = "Combo",
				dataMapping = @DataMapping {
				forArray = false,
				mappings = [MappingKind.TYPE_PRIMITIVE_ALL]},
				genController = true,
				purposes = [PurposeKind.FOR_DISPLAY, PurposeKind.FOR_CREATE, PurposeKind.FOR_UPDATE],
				template = "${widgetName} ${typeName:org.eclipse.edt.rui.widgets.Combo} {};"
			}
		]
	}} 

	values String[] { @EGLProperty { getMethod=getValues, setMethod=setValues }, @VEProperty{} };
	selection Int {	@EGLProperty { getMethod=getSelection, setMethod=setSelection }, @VEProperty{} };
	private reverseTextDirectionThis String = "No";
	private textLayoutThis String = "Logical";
	private history HistoryHelper{};
	
	private options Widget[];
	
	function setValues(value string[] in)
		values = value;
		removeChildren();
		options.removeAll();
		for(index int from 1 to value.getSize() by 1)
			option Widget = new Widget{tagName = "option"};
			option.innerHTML = value[index];
			options.appendElement(option);
			appendChild(option);
		end
		setSelection(1);

		if(this.textLayoutThis == "Visual" || this.reverseTextDirectionThis == "Yes")		
			setBiDiMarkers();
		end

		if ( history.isIE() && this.logicalParent isa Box )
			(this.logicalParent as Box).layout();
		end
	end
	
	function getValues() returns (string[])
		return (values);
	end
	
	function setSelection(selection int in)
		if (selection > 0 && selection <= options.getSize())
			options[selection].setAttribute("selected", "true");
		end
	end
	
	function getSelection() returns (int)
		return ((getAttribute("selectedIndex") as int) + 1);
	end
	
	function setTextLayout(textLayout String in) 
		this.textLayoutThis = textLayout;	
		setBiDiMarkers();
	end
	
	function getTextLayout() returns (String) 
		return (this.textLayoutThis);
	end

	function setReverseTextDirection(reverseTextDirection String in) 
		this.reverseTextDirectionThis = reverseTextDirection;			
		setBiDiMarkers();
	end
	
	function getReverseTextDirection() returns (String) 
		return (this.reverseTextDirectionThis);
	end
		
	private function setBiDiMarkers()
		isVisual boolean = this.textLayoutThis == "Visual";
		isReverseDirection boolean = this.reverseTextDirectionThis == "Yes";		
		for(index int from 1 to options.getSize() by 1)			
			options[index].setBiDiMarkers(isVisual,isReverseDirection);
		end
	end	
	
	private function setValueAsText(value String in)
		if(values.getSize() < 1)
			return;
		end
		for(index int from 1 to values.getSize() by 1)
			if(value == values[index])
				setSelection(index);
				return;
			end
		end
		if(values.getSize()>0)
			setSelection(1);	
		end
	end
	
	private function getValueAsText() returns(String)
		if(values.getSize() < 1)
			return (null);
		end
		return (values[getSelection()]);
	end
end
